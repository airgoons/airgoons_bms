// Kuwait Theater - Desert Storm scenario
//
//
// Base event file
//
// :::::::::::::::::::::::::::::::::::::::::;
//
// #TOTAL_EVENTS is a required entry, and
// should be # of events we have triggers for
// and code fore 
// KCK WARNING: KEEP THIS UPDATED!
//
#TOTAL_EVENTS 14
//
// :::::::::::::::::::::::::::::::::::::::::;
// This marks the Coalition as initially on the offensive (9) or defensive (10)
#SET_EVENT 10
//
// This sets the initial tempo
#SET_TEMPO 150
#ENDINIT
//
//
// Starts with a couple news reports about Iraq invading Kuwait, and then Iraq capturing Kuwait City
// #PLAY_MOVIE does this intentionally
//
// :::::::::::::::::::::::::::::::::::::::::;
// 
// Now come the individual triggers
// 
// Event #1 Khafji being attacked by Iraq forces
// (Border Checkpoint marker in North Khafji Id (2351), Al Olaya town Id (854)
// (Pack Al Khafji 420) (Pack As Salman 369)
#IF_NOT_EVENT_PLAYED 1
#IF_CONTROLLED 6 OR 2351 854
#PLAY_MOVIE 100
#SET_PAK_PRIORITY 6 420 100
#SET_PAK_PRIORITY 2 420 100
#SET_PAK_PRIORITY 6 418 95
#SET_PAK_PRIORITY 2 418 95
#CHANGE_PRIORITIES 6 2
#CHANGE_PRIORITIES 2 1
#SET_TEMPO 255
#DO_EVENT 1
#DO_MONOLOG Khafji being attacked by Iraq forces
#ENDIF
#ENDIF
//
// Event #2 Khafji City (Pack 420) falls/regained
// Khafji city Id 420 and near Khafji town Id(1234)
#IF_EVENT_PLAYED 2
#IF_CONTROLLED 2 AND 420 1234
#SHIFT_INITIATIVE 2 6 50
#RESET_EVENT 2
#DO_MONOLOG Khafji City regained
#ENDIF
#ELSE
#IF_CONTROLLED 6 AND 420 1234
#PLAY_MOVIE 101
#DO_EVENT 2
#SHIFT_INITIATIVE 6 2 50
#DO_MONOLOG Khafji City falls
#ENDIF
#ENDIF
// 
// Event #3
// Kuwait (11) being attacked by Coalition forces - As-Sullayyil City (75) and Al Wafrah (26)
// 
#IF_NOT_EVENT_PLAYED 3
#IF_CONTROLLED 2 OR 75 26
#PLAY_MOVIE 102
#SET_PAK_PRIORITY 6 11 100
#SET_PAK_PRIORITY 2 11 100
#CHANGE_PRIORITIES 6 1
#CHANGE_PRIORITIES 2 2
#SET_TEMPO 255
#DO_EVENT 3
#DO_MONOLOG Kuwait being attacked by Coalition forces
#ENDIF
#ENDIF
// 
// Event #4
// Kuwait (11) falls/regained
// Kuwait ID (11), Al Jahra ID (8)
#IF_EVENT_PLAYED 4
#IF_CONTROLLED 6 AND 11 8
#SHIFT_INITIATIVE 2 6 50
#RESET_EVENT 4
#DO_MONOLOG Kuwait regained
#ENDIF
#ELSE
#IF_CONTROLLED 2 AND 11 8
#PLAY_MOVIE 103
#SET_PAK_PRIORITY 6 365 100
#SET_PAK_PRIORITY 2 365 100
#DO_EVENT 4
#SHIFT_INITIATIVE 6 2 50
#DO_MONOLOG Kuwait liberated
#ENDIF
#ENDIF
//
// Event #5 (Third line objects)
// Iraq advanced beyond border Al Olaya Depot Id (1329),Saudi Depot(1255), Ar Ruqi Id (417), Al Naziriyah Depot (918), Ash Shubah town (2459), Rawdat Habbas (2378) Rafha Airbase 388, /// WAS Rafha(412), Hafar Al Batin (418), Khafji National Guard (880) WAS King Khalid MC (430), Al Khawwari (727)
//
#IF_NOT_EVENT_PLAYED 5
#IF_NOT_EVENT_PLAYED 7
#IF_CONTROLLED 6 OR 1329 1255 417 918 2459 2378 388
#DO_EVENT 5
#RESET_EVENT 6
#DO_MONOLOG Iraq advanced beyond border
#ENDIF
#ENDIF
#ENDIF
// 
// Event #6 (First line objects)
// Iraq pushed back to border Saudi Border(2351),Al Olaya town(854), Saudi Road Junction(1250), Saudi Highway(2080), Saudi Border Crossing Id (1353),DMZ Road Junction(1135), Saudi Highway (2131)///WAS Rafha (412), Hafar Al Batin (418), Khafji National Guard (880) WAS  Ar Ruqi (417) Kuwait Highway Marker (81)
// 
#IF_EVENT_PLAYED 5
#IF_EVENT_PLAYED 7
#PLAY_MOVIE 104
#DO_EVENT 6
#RESET_EVENT 5
#DO_MONOLOG Iraq pushed back to border
#ELSE
#IF_CONTROLLED 2 AND 2351 854 1250 2080 1353 1135 2131
#PLAY_MOVIE 104
#DO_EVENT 6
#RESET_EVENT 5
#DO_MONOLOG Iraq pushed back to DMZ
#ENDIF
#ENDIF
#ENDIF
// 
// Event #7 (Third line objects)
// Combined forces advanced beyond Iraq border
// Al Khiran(72), Mahboula village (74), Umm Al Roos town(1357), Border Checkpoint (2527) Qurech int(2448), Iraqi Highway(2060), Iraqi Depot(1317)
#IF_NOT_EVENT_PLAYED 7
#IF_NOT_EVENT_PLAYED 5
#IF_CONTROLLED 2 OR 72 74 1357 2527 2448 2060 1317
#DO_EVENT 7
#RESET_EVENT 8
#DO_MONOLOG Combined forces advanced beyond border
#ENDIF
#ENDIF
#ENDIF
// 
// Event #8
// Combined forces pushed back to border Kuwait  Border Checkpoint (894), Najran (1358), Ross chemical (426), Kuwait  Border Checkpoint(2529), Iraqi Highway(2126), DMZ Road Junction(2085), Iraqi Border Checkpoint (1112)
// 
#IF_EVENT_PLAYED 7
#IF_EVENT_PLAYED 5
#PLAY_MOVIE 105
#DO_EVENT 8
#RESET_EVENT 7
#DO_MONOLOG Combined forces pushed back to border
#ELSE
#IF_CONTROLLED 6 AND 894 1358 426 2529 2126 2085 1112
#PLAY_MOVIE 105
#DO_EVENT 8
#RESET_EVENT 7
#DO_MONOLOG Combined forces pushed back to border
#ENDIF
#ENDIF
#ENDIF
// 
// Event #9
// Combined forces go on offensive
// 
#IF_EVENT_PLAYED 10
#IF_ON_OFFENSIVE 2
#PLAY_MOVIE 106
#DO_EVENT 9
#RESET_EVENT 10
#DO_MONOLOG Combined forces go on offensive
#ENDIF
#ENDIF
// 
// Event #10
// Combined forces go on defensive
// 
#IF_EVENT_PLAYED 9
#IF_ON_OFFENSIVE 6
#PLAY_MOVIE 107
#DO_EVENT 10
#RESET_EVENT 9
#DO_MONOLOG Combined forces go on defensive
#ENDIF
#ENDIF
// 
// Event #11
// Bad guys win
// Basically - King Khalid MC (430), Hafar Al Batin (418), Ad Damman (510)
//
//
#IF_CONTROLLED 6 AND 418 510 430
#PLAY_MOVIE 113
#DO_EVENT 14
#END_GAME 14
#ENDIF
// 
// Event #12
// Stalemate
// 
#IF_BORDOM_HOURS 240
#PLAY_MOVIE 114
#DO_EVENT 15
#END_GAME 15
#ENDIF
// 
// Event #13
// Timeout - You have 30 days to win (or lose) the war
// 
#IF_CAMPAIGN_DAY G 30
#PLAY_MOVIE 115
#DO_EVENT 16
#END_GAME 16
#ENDIF
// 
// Event #14
// Good guys win
// Basically - Al Basrah (365) Basrah village (364) Al Hayyaniyah (363)
//
#IF_CONTROLLED 2 AND 363 364 365
#PLAY_MOVIE 116
#DO_EVENT 17
#END_GAME 17
#ENDIF
// End
#ENDSCRIPT
